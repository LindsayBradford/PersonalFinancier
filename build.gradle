apply plugin: 'java'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

project.group = "blacksmyth.personalfinancier"
project.version = "0.1"

// Storing all dependencies in a libs directory. 

repositories {
   flatDir {
     dirs 'libs'  
   }
}

dependencies {
  compile 'jtattoo:JTattoo:1.3'
  compile 'gson:gson:2.1'
  compile 'gral-core:gral-core:0.9'
}

// modify the sourceSets to match the layout of my java-only project.

sourceSets {
  main {
    java {
      srcDir 'src'
    }
    resources {
      srcDir 'src/blacksmyth/resources'
    }
  }
//  output.resourcesDir = 'build/classes/main/blacksmyth/resources'
}

// Sub-par.  How can I set this nicely from within sourceSets?
// the sourceset command above is parsed, but doesn't seem to have
// effect at processResources task execution time.

processResources {
 destinationDir = file('build/classes/main/blacksmyth/resources')
}

jar {
  // Merge jars we're dependent on into the executable jar.

  from { 
    configurations.compile.collect { 
      it.isDirectory() ? it : zipTree(it)
    } 
  }

  manifest {
    attributes 'Main-Class' : 'blacksmyth.personalfinancier.PersonalFinancier'
  }
}


// the java plugin task uploadArchives, modified as below is equivalent 
// to my ant script "dist" target. So, I just got lazy and made a dist
// task dependent on a slightly modified uploadArchives.

uploadArchives {
  repositories {
    flatDir {
      dirs 'dist'
    }
  }
}

task dist {
  dependsOn uploadArchives
}

// the full task is the gradle equivalent of the ant script's 
// full target.

task full {
  dependsOn clean, dist
}
